# ====================================================================================
# PEG Grammar for 'some language, idk anymore'
# ====================================================================================

# ---- Top-level statement - This is the entry point ----

test
    <- program

program
    <- Spacing postgresql_statement EndOfFile

postgresql_statement
    <- (create_function / create_procedure) SEP



# ------------------------------------------------------------------------------------
# CREATE FUNCTION Statement
# ------------------------------------------------------------------------------------

create_function
    <-  CREATE (OR REPLACE)? FUNCTION function_name function_arglist
        (RETURNS type 
        / RETURNS TABLE column_list)?
        function_options

function_name 
    <- name

function_arglist 
    <- OPEN (function_arg (COMMA function_arg)*)? CLOSE

function_arg 
    <- argmode? argname? argtype ((EQUALS / DEFAULT) default_expr)?

argmode 
    <- IN / OUT/ INOUT / VARIADIC

argname 
    <- name 

argtype 
    <- type 

default_expr 
    <- expression

column_list 
    <- OPEN column (COMMA column)* CLOSE

column 
    <- column_name column_type

column_name 
    <- name

column_type
    <- type

function_options
    <- function_option+

function_option
    <- (LANGUAGE language 
    / transform_option
    / WINDOW 
    / volatility_option
    / leakproof_option
    / nullinput_option
    / security_option
    / parallel_option
    / COST execution_cost
    / ROWS result_rows
    / SUPPORT support_function
    / set_option
    / AS definition
    )

transform_option
    <- TRANSFORM transform_list 

transform_list 
    <- transformation (COMMA transformation)*

transformation
    <- FOR TYPE type 

volatility_option
    <- IMMUTABLE / STABLE / VOLATILE

leakproof_option
    <- NOT? LEAKPROOF 

nullinput_option
    <- CALLED ON NULL INPUT / RETURNS NULL ON NULL INPUT / STRICT

security_option 
    <- EXTERNAL? SECURITY INVOKER / EXTERNAL? SECURITY DEFINER

parallel_option
    <- PARALLEL (UNSAFE / RESTRICTED / SAFE)

set_option 
    <- SET configuration_parameter (TO expression / EQUALS expression / FROM CURRENT)

execution_cost
    <- positive_real # TODO: zu 'expression' blackboxen (nervt aber beim testen)

result_rows 
    <- positive_real # TODO: zu 'expression' blackboxen (nervt aber beim testen)

support_function
    <- function_name

configuration_parameter
    <- parameter_name

parameter_name 
    <- identifier

definition 
    <- block_start plpgsql_block block_end 

block_start
    <- DOUBLE_DOLLAR
    
block_end
    <- DOUBLE_DOLLAR


# ------------------------------------------------------------------------------------
# CREATE PROCEDURE Statement
# ------------------------------------------------------------------------------------

create_procedure
    <-  CREATE (OR REPLACE)? PROCEDURE procedure_name function_arglist
        procedure_options

procedure_name 
    <- name

procedure_options
    <- procedure_option+

procedure_option
    <- transform_option
    / security_option
    / set_option
    / AS definition


# ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
# PL/pgSQL Statements
# ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

plpgsql_block
    <- label? declare_section? BEGIN plpgsql_statement_list END label? SEP 

label 
    <- LSHIFT name RSHIFT   # TODO: note that <<   blubb   >> will have the name 'blubb' without spaces


# ------------------------------------------------------------------------------------
# Identifiers, Types and Expressions
# ------------------------------------------------------------------------------------

name
    <- identifier

identifier 
    <- (quoted_id / id) Spacing

quoted_id
    <- '"' ( '""' / (!'"' .) )* '"'

id  <- (!keyword) idStart idChars* 

idStart
    <- [a-zA-Z_]

idChars 
    <- [a-zA-Z0-9_]

positive_real 
    <- [0-9]+ ('.' [0-9]*)? Spacing  

expression 
    <- any    # TODO: blackbox this (use identifier for testing purposes)    

type 
    <- any   # TODO: blackbox this (use identifier for testing purposes) 

any 
    <- identifier / number 

number 
    <- [0-9]+ ('.' [0-9]+)? Spacing


# ------------------------------------------------------------------------------------
# Keywords
# ------------------------------------------------------------------------------------

keyword <- CREATE / PROCEDURE / LANGUAGE / PLPGSQL / AS 
/ SECURITY / INVOKER / DEFINER / SET / CALL / IN / OUT 
/ INOUT / BEGIN / END / DECLARE / FUNCTION / OR / REPLACE
/ RETURNS / TABLE / VARIADIC / TRANSFORM / FOR / TYPE
/ WINDOW / IMMUTABLE / STABLE / VOLATILE / NOT
/ LEAKPROOF / CALLED / ON / NULL / INPUT / STRICT 
/ EXTERNAL / PARALLEL / COST / ROWS / SUPPORT
/ TO / FROM / CURRENT / UNSAFE / RESTRICTED / SAFE / DEFAULT / CONSTANT


CREATE      <- [Cc] [Rr] [Ee] [Aa] [Tt] [Ee] keysep
PROCEDURE   <- [Pp] [Rr] [Oo] [Cc] [Ee] [Dd] [Uu] [Rr] [Ee] keysep
LANGUAGE    <- [Ll] [Aa] [Nn] [Gg] [Uu] [Aa] [Gg] [Ee] keysep
AS          <- [Aa] [Ss] keysep
SECURITY    <- [Ss] [Ee] [Cc] [Uu] [Rr] [Ii] [Tt] [Yy] keysep
INVOKER     <- [Ii] [Nn] [Vv] [Oo] [Kk] [Ee] [Rr] keysep
DEFINER     <- [Dd] [Ee] [Ff] [Ii] [Nn] [Ee] [Rr] keysep
SET         <- [Ss] [Ee] [Tt] keysep
CALL        <- [Cc] [Aa] [Ll] [Ll] keysep
IN          <- [Ii] [Nn] keysep
OUT         <- [Oo] [Uu] [Tt] keysep
INOUT       <- [Ii] [Nn] [Oo] [Uu] [Tt] keysep
BEGIN       <- [Bb] [Ee] [Gg] [Ii] [Nn] keysep
END         <- [Ee] [Nn] [Dd] keysep
DECLARE     <- [Dd] [Ee] [Cc] [Ll] [Aa] [Rr] [Ee] keysep
FUNCTION    <- [Ff] [Uu] [Nn] [Cc] [Tt] [Ii] [Oo] [Nn] keysep
OR          <- [Oo] [Rr] keysep
REPLACE     <- [Rr] [Ee] [Pp] [Ll] [Aa] [Cc] [Ee] keysep
RETURNS     <- [Rr] [Ee] [Tt] [Uu] [Rr] [Nn] [Ss] keysep
TABLE       <- [Tt] [Aa] [Bb] [Ll] [Ee] keysep
VARIADIC    <- [Vv] [Aa] [Rr] [Ii] [Aa] [Dd] [Ii] [Cc] keysep
TRANSFORM   <- [Tt] [Rr] [Aa] [Nn] [Ss] [Ff] [Oo] [Rr] [Mm] keysep
FOR         <- [Ff] [Oo] [Rr] keysep
TYPE        <- [Tt] [Yy] [Pp] [Ee] keysep
WINDOW      <- [Ww] [Ii] [Nn] [Dd] [Oo] [Ww] keysep
IMMUTABLE   <- [Ii] [Mm] [Mm] [Uu] [Tt] [Aa] [Bb] [Ll] [Ee] keysep
STABLE      <- [Ss] [Tt] [Aa] [Bb] [Ll] [Ee] keysep
VOLATILE    <- [Vv] [Oo] [Ll] [Aa] [Tt] [Ii] [Ll] [Ee] keysep
NOT         <- [Nn] [Oo] [Tt] keysep
LEAKPROOF   <- [Ll] [Ee] [Aa] [Kk] [Pp] [Rr] [Oo] [Oo] [Ff] keysep
CALLED      <- [Cc] [Aa] [Ll] [Ll] [Ee] [Dd] keysep
ON          <- [Oo] [Nn] keysep
NULL        <- [Nn] [Uu] [Ll] [Ll] keysep
INPUT       <- [Ii] [Nn] [Pp] [Uu] [Tt] keysep
STRICT      <- [Ss] [Tt] [Rr] [Ii] [Cc] [Tt] keysep
EXTERNAL    <- [Ee] [Xx] [Tt] [Ee] [Rr] [Nn] [Aa] [Ll] keysep
PARALLEL    <- [Pp] [Aa] [Rr] [Aa] [Ll] [Ll] [Ee] [Ll] keysep
COST        <- [Cc] [Oo] [Ss] [Tt] keysep
ROWS        <- [Rr] [Oo] [Ww] [Ss] keysep
SUPPORT     <- [Ss] [Uu] [Pp] [Pp] [Oo] [Rr] [Tt] keysep
TO          <- [Tt] [Oo] keysep
FROM        <- [Ff] [Rr] [Oo] [Mm] keysep
CURRENT     <- [Cc] [Uu] [Rr] [Rr] [Ee] [Nn] [Tt] keysep
UNSAFE      <- [Uu] [Nn] [Ss] [Aa] [Ff] [Ee] keysep
RESTRICTED  <- [Rr] [Ee] [Ss] [Tt] [Rr] [Ii] [Cc] [Tt] [Ee] [Dd] keysep
SAFE        <- [Ss] [Aa] [Ff] [Ee] keysep
DEFAULT     <- [Dd] [Ee] [Ff] [Aa] [Uu] [Ll] [Tt] keysep
CONSTANT    <- [Cc] [Oo] [Nn] [Ss] [Tt] [Aa] [Nn] [Tt] keysep
PLPGSQL     <- [Pp] [Ll] [Pp] [Gg] [Ss] [Qq] [Ll] keysep

language 
    <- PLPGSQL
    / 'other potential language'


# ------------------------------------------------------------------------------------
# Meta Keywords
# ------------------------------------------------------------------------------------

keysep 
    <- !idChars Spacing

SEP     <- ';' Spacing
OPEN    <- '(' Spacing
CLOSE   <- ')' Spacing 
COMMA   <- ',' Spacing
DOLLAR  <- '$' Spacing
DOUBLE_DOLLAR 
        <- '$$' Spacing
ASSIGN  <- ':=' Spacing
EQUALS  <- '=' Spacing
LSHIFT  <- '<<' Spacing
RSHIFT  <- '>>' Spacing


# ------------------------------------------------------------------------------------
# Whitespace, Comments and Aux
# ------------------------------------------------------------------------------------

Spacing 
    <- (Space / Comment / EndOfLine)*

Space 
    <- ' ' 
    / '\t'

Comment 
    <- '--' ((!EndOfLine) .)* (EndOfLine / EndOfFile)
    / '/*' (!'*/' .)* '*/'

EndOfLine 
    <- '\r\n'
    / '\n'
    / '\r'

EndOfFile
    <- !.

blackbox 
    <- DOLLAR (!DOLLAR .)* DOLLAR Spacing







